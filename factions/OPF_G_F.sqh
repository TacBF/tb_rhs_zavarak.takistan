// RES - Taliban
// Author: Cole

#include "\ice\ice_gear\kitDefines.sqh"

private ["_result", "_role", "_side", "_faction",
	"_weapons", "_attachments", "_magazines", "_commonOrdnance",
	"_uniform", "_vest", "_backpack",
	"_uniformItems", "_vestItems", "_backpackItems",
	"_assignedItems", "_helmet", "_glasses", "_nvGoggles", "_binoculars", "_nvGogglesForAll"];

_role = _this select 0;

//====================================================//
//				  Default Loadout                     // 
//====================================================//

	// Up to 3 weapons = ["primary", "secondary", "handgun"]
	_weapons = ["rhs_weap_akm"];
	// Up to 3 attachments per weapon = [["primary"], ["secondary"], ["handgun"]]
	_attachments = [[], [], []];
	
	// Magazines for all weapons, unlimited array, added to any container with space available.
	_magazines = [["rhs_30Rnd_762x39mm", 8]];
	// Common gear items, these will be added to any container with space available.
	_commonOrdnance = [["rhs_mag_rgd5", 2],["SmokeShell", 2],["ICE_FieldDressing", 4],["ICE_Adrenaline", 2],["ICE_Morphine", 1]];
	
	// Containers
	_uniform = ["U_BG_Guerrilla_6_1", "U_BG_Guerilla1_1", "U_BG_Guerilla2_2", "U_BG_Guerilla2_3", "U_BG_Guerilla3_1", "U_BG_Guerilla3_2", "U_BG_leader", "U_BG_Guerilla2_1"] call BIS_fnc_selectRandom;
	_vest = ["V_Chestrig_oli", "V_Chestrig_blk", "V_Chestrig_rgr", "V_Chestrig_khk"] call BIS_fnc_selectRandom;
	_backpack = "";

	_helmet = ["", "H_Shemag_khk", "H_Shemag_olive", "H_Shemag_tan", "H_Bandanna_gry", "H_Bandanna_cbr", "H_Bandanna_sand","H_Bandanna_khk", "H_Bandanna_sgg", "H_Bandanna_blu", "H_Cap_red", "H_Cap_blu", "H_Cap_oli","H_Cap_tan", "H_Cap_blk"] call BIS_fnc_selectRandom;
	_glasses = ["", "rhs_scarf", "G_Bandanna_blk", "G_Bandanna_beast", "G_Bandanna_khk", "G_Bandanna_oli", "G_Bandanna_tan"] call BIS_fnc_selectRandom;
	_assignedItems = ["ItemMap", "ItemRadio", "ItemCompass", "ItemWatch"];
	_nvGoggles = if (__isNight) then {"NVGoggles";} else {"";};
	_binoculars = "Binocular";
	
	// optional alternate method, which allows you to list items specifically for this container type. Otherwise use "_magazines" and/or "_commonOrdnance" instead. If using both, ensure you do not double up items.
	_uniformItems = [];
	_vestItems = [];
	_backpackItems = [];

//====================================================//
//					Kit Overrides                     // 
//====================================================//

switch (_role) do
{
	case __ICE_gear_Rifleman:
	{
		_backpack = "TB_Hideout_Pack_East";
	};
	
	case __ICE_gear_Grenadier:
	{
		_weapons = ["rhs_weap_akm_gp25"];
		_vest = ["V_HarnessOGL_gry", "V_HarnessOGL_brn"] call BIS_fnc_selectRandom;
		_magazines = if (__isNight) then {[["rhs_30Rnd_762x39mm", 8], ["rhs_VOG25", 8], ["rhs_VG40OP_red", 4]];} else {[["rhs_30Rnd_762x39mm", 8], ["rhs_VOG25", 8], ["rhs_GRD40_White", 4]];};
		_backpack = "TB_Hideout_Pack_East";
	};
	
	case __ICE_gear_MG:
	{
		_weapons = ["rhs_weap_pkm"];
		_backpack = ["B_Carryall_cbr", "B_Carryall_khk", "B_Carryall_oli"] call BIS_fnc_selectRandom;
		_magazines = [["rhs_100Rnd_762x54mmR", 5]];
	};
	
	case __ICE_gear_Medic:
	{
		_commonOrdnance = [["rhs_mag_rgd5", 2], ["SmokeShell", 8], ["ICE_FieldDressing", 20], ["ICE_Adrenaline", 20], ["ICE_Morphine", 10]];
		_backpack = "TB_Hideout_Pack_East";
	};
	
	case __ICE_gear_CombatEngineer:
	{
		_weapons = ["rhs_weap_M590_8RD"];
		_magazines = [["rhsusf_8Rnd_00Buck", 8]];
		_backpack = ["B_Carryall_cbr", "B_Carryall_khk", "B_Carryall_oli"] call BIS_fnc_selectRandom;
		_vest = ["V_HarnessO_brn", "V_HarnessO_gry"] call BIS_fnc_selectRandom;
		_backpackItems = [["CUP_IED_V1_M", 1]];
	};
	
	case __ICE_gear_LightAT:
	{
		_weapons pushback "rhs_weap_rpg26";
		_backpack = "TB_Hideout_Pack_East";
	};
	
	case __ICE_gear_HeavyAT:
	{
		_weapons pushback "rhs_weap_rpg7";
		_attachments set [1, ["rhs_acc_pgo7v3"]];
		_magazines pushback ["rhs_rpg7_PG7VL_mag", 3];
		_backpack = ["B_Carryall_cbr", "B_Carryall_khk", "B_Carryall_oli"] call BIS_fnc_selectRandom;
	};
	
	case __ICE_gear_AA:
	{
		_weapons pushback "rhs_weap_fim92";
		_magazines pushback ["rhs_fim92_mag", 1];
		_backpack = ["B_Carryall_cbr", "B_Carryall_khk", "B_Carryall_oli"] call BIS_fnc_selectRandom;
	};
	
	case __ICE_gear_Marksman:
	{
		_weapons = ["rhs_weap_svds"];
		_attachments = [["rhs_acc_pso1m2"], [], []];
		_magazines = [["rhs_10Rnd_762x54mmR_7N1", 8]];
		_binoculars = "rhs_pdu4";
	};
	
	case __ICE_gear_Sniper:
	{
		_weapons = ["rhs_weap_svdp_wd_npz", "rhs_weap_makarov_pm"];
		_attachments = [["optic_LRPS"], [], []];
		_magazines = [["rhs_10Rnd_762x54mmR_7N1", 8], ["rhs_mag_9x18_12_57N181S", 3]];
		_assignedItems pushback "ItemGPS";
		_commonOrdnance pushback ["SmokeShellYellow", 2];
		_binoculars = "rhs_pdu4";
		_vest = ["V_BandollierB_khk", "V_BandollierB_cbr", "V_BandollierB_rgr", "V_BandollierB_blk", "V_BandollierB_oli"] call BIS_fnc_selectRandom;
	};
	
	case __ICE_gear_SL:
	{
		_backpack = "TB_Hideout_Pack_East";
		_assignedItems pushback "ItemGPS";
		_commonOrdnance pushback ["SmokeShellYellow", 2];
		_binoculars = "rhs_pdu4";
	};
	
	case __ICE_gear_Crewman:
	{
		_magazines = [["rhs_30Rnd_762x39mm", 5]];
		_assignedItems pushback "ItemGPS";
		_vest = ["V_BandollierB_khk", "V_BandollierB_cbr", "V_BandollierB_rgr", "V_BandollierB_blk", "V_BandollierB_oli"] call BIS_fnc_selectRandom;
		_commonOrdnance pushback ["SmokeShellYellow", 2];
		_binoculars = "rhs_pdu4";
	};
	
	case __ICE_gear_Pilot:
	{
		_magazines = [["rhs_30Rnd_762x39mm", 5]];
		_assignedItems pushback "ItemGPS";
		_helmet = "rhs_zsh7a_mike";
		_vest = ["V_BandollierB_khk", "V_BandollierB_cbr", "V_BandollierB_rgr", "V_BandollierB_blk", "V_BandollierB_oli"] call BIS_fnc_selectRandom;
		_commonOrdnance pushback ["SmokeShellYellow", 2];
		_backpack = "B_Parachute";
	};
	
	case __ICE_gear_Diver:
	{
		_backpack = "TB_SuicideBomb_Pack";
		_backpackItems = [["DemoCharge_Remote_Mag", 3]];
	};
};

//-----------------------------------------------------------------------------
_nvGogglesForAll = [["gear", "NVGogglesForAll"], "resistance", 1, true] call ICE_fnc_getSetting;
if (_nvGogglesForAll == 0) then {_nvGoggles = "";};
if (_nvGoggles != "") then { _assignedItems pushBack _nvGoggles };
if (_binoculars != "") then { _assignedItems pushBack _binoculars };
//-----------------------------------------------------------------------------
_result =
[
	_weapons,
	_attachments, // nested array
	_magazines, // nested array - in A3, all magazines should be in each container's items list.
	_commonOrdnance, // combined nested/single array - in A3, all magazines should be in each container's items list.

	_uniform,
	_vest,
	_backpack,

	_uniformItems,
	_vestItems,
	_backpackItems,

	_assignedItems,
	_helmet,
	_glasses
];
//-----------------------------------------------------------------------------
_result